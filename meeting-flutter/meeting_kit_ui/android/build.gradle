// Copyright (c) 2022 NetEase, Inc. All rights reserved.
// Use of this source code is governed by a MIT license that can be
// found in the LICENSE file.

group 'com.netease.meeting.plugin.meeting_plugin'
version '1.0'

buildscript {
    ext.kotlin_version = '1.6.10'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    google()
    mavenCentral()
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 21
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    def rtc_version = rootProject.hasProperty('nertc_version')
            ? rootProject.property('nertc_version') as String
            : '5.3.7'
    println "use nertc version: $rtc_version"
    [
            'base',
            'beauty',
            'facedetect',
            'segment',
            'aidenoise',
            'aihowling',
            'nenn',
            'videodenoise',
            'faceenhance',
            'superresolution',
    ].forEach { name ->
        implementation("com.netease.yunxin:nertc-$name:$rtc_version") {
            changing = rtc_version.endsWith('SNAPSHOT')
        }
    }
    def lifecycle_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"
}
